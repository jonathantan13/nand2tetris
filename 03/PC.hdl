// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/a/PC.hdl
/**
 * A 16-bit counter.
 * if      reset(t): out(t+1) = 0
 * else if load(t):  out(t+1) = in(t)
 * else if inc(t):   out(t+1) = out(t) + 1
 * else              out(t+1) = out(t)
 */
CHIP PC {
    IN in[16], reset, load, inc;
    OUT out[16];
    
    PARTS:
    // Incrementor (takes output and increments 1 bit)
    Add16(a=outa, b[0]=true, b[1..15]=false, out=outadd);

    // Choose between the output and incrementing
    Mux16(a=outa, b=outadd, sel=inc, out=outinc);

    // Choose between output/incrementing or loading a value given by "in"
    Mux16(a=outinc, b=in, sel=load, out=outload);

    // Choose between the previous steps or to reset (PC = 0) 
    Mux16(a=outload, b=false, sel=reset, out=outreset);

    // Optimization: Loading only if one of the 3 loaders is called
    Or(a=inc, b=load, out=load1);
    Or(a=load1, b=reset, out=load2);

    // Store state in a register
    Register(in=outreset, load=load2, out=outa, out=out);
}